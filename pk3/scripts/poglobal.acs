#library "POGLOBAL"
#include "zcommon.acs"

#define TURN_RATE 0.025
#define STEP_RATE 8
#define ANGLE_QUARTER_TURN 0.25
#define TILE_SIZE 128

#define ENCOUNTER_TEXT_X 400.0
#define ENCOUNTER_TEXT_Y 200.0
#define ENCOUNTER_NAME_Y_OFFSET 60.0

//These are used to simulate passing multiple ints back from getPositionFromSquare
int registerX = 0;
int registerY = 0;
int registerZ = 0;

int showingInventory = 0;
int levelNum;

int currentAngle;
int squareNum;

//Cheats/debug
script "poarsenal" (int type, int element) {
    str weapons[5] = {"Pistol", "Shotgun", "Chaingun", "RocketLauncher", "PlasmaRifle"};
    str typestring = weapons[type];
    str elements[5] = {"None", "Fire", "Ice", "Lightning", "Poison"};
    str elementstring = elements[element];
    ScriptCall("DataLibrary", "AddWeapon", typestring, elementstring);
}

script "powish" (int type) {
    str items[4] = {"MFIMegasphere", "MFIRadsuit", "MFIMedikit", "MFIStimpack"};
    str typestring = items[type];
    ScriptCall("DataLibrary", "InventoryAdd", typestring, -1);
}

script "return" RETURN {
    FadeTo(0.0, 0.0, 0.0, 1.0, 0.0);
    HudMessageBold(s:""; HUDMSG_PLAIN, 999, CR_UNTRANSLATED, 0, 0, 0, 0);
    HudMessageBold(s:""; HUDMSG_PLAIN, 998, CR_UNTRANSLATED, 0, 0, 0, 0);
	ACS_NamedExecuteAlways("enter", 0);
}

script "enter" ENTER {
    int mapType = ScriptCall("DataLibrary", "LoadMapProperties");
    levelNum = GetLevelInfo(LEVELINFO_LEVELNUM);
    FadeTo(0.0, 0.0, 0.0, 1.0, 0.0);
    if (mapType == 1) {
        SetMusic("POSILENT");
        SetPlayerProperty(1, 1, PROP_FROZEN);
        ScriptCall("LevelHelper", "StashWeapons");
        TakeInventory("PoDanger", CheckInventory("PoDanger"));

        ScriptCall("DataLibrary", "WriteData", "FieldMap", StrParam(d:levelNum));
        //If we're coming back from a FOE fight, destroy the FOE we marked
        ScriptCall("FOEHelper", "DestroyCurrentFOE");

        int startingSquare = ScriptCall("DataLibrary", "ReadInt", "FieldSquare");
        if (startingSquare > 0) {
            getPositionFromSquare(startingSquare);
            SetActorPosition(0, registerX, registerY, registerZ, false);
            int angleToSet = ScriptCall("DataLibrary", "ReadInt", "FieldAngle");
            SetActorAngle(0, angleToSet);
        }

        //OK, now fade in and start listening for movement!
        FadeTo(0.0, 0.0, 0.0, 0.0, 1.0);
        ACS_NamedExecute("rpgMovement", 0);
    } else if (mapType == 2) {
        ACS_NamedExecute("setupArena", 0);
    } else {
        Log(s:"ERROR: No map type");
    }
}

script "openChest" (int chestNumber) {
	SetActorState(0, "Opened");
}

script "listenInventory" ENTER {
    while (true) {
		int buttons = GetPlayerInput(0, INPUT_BUTTONS);
		int oldButtons = GetPlayerInput(0, INPUT_OLDBUTTONS);
        if (buttons & BT_USER2 || !oldButtons & BT_USER2) {
            showingInventory = ScriptCall("DataLibrary", "ReadInt", "showInvScreen");
            showingInventory = !showingInventory;
            PlaySound(0, "po/inventory/open", CHAN_AUTO);
            ScriptCall("DataLibrary", "WriteData", "showInvScreen", StrParam(d:showingInventory));
            delay(2);
        }
        delay(2);
    }
}

script "rpgMovement" (void) {

    delay(15);
    SetMusic("POEXPLR");

	int i;
	while (true) {
		delay(1);
		int buttons = GetPlayerInput(0, INPUT_BUTTONS);
		int oldButtons = GetPlayerInput(0, INPUT_OLDBUTTONS);
		if (buttons & BT_LEFT) {
			SetActorAngle(0, quantizeAngle(GetActorAngle(0)));
			int targetAngle = GetActorAngle(0) + 0.25;
			for (i = 0; i < ANGLE_QUARTER_TURN/TURN_RATE; i++) {
				ChangeActorAngle(0, GetActorAngle(0) + TURN_RATE);
				delay(1);
			}
			ChangeActorAngle(0, targetAngle);
			delay(2);
		}
		if (buttons & BT_RIGHT) {
			SetActorAngle(0, quantizeAngle(GetActorAngle(0)));
			targetAngle = GetActorAngle(0) - 0.25;
			for (i = 0; i < ANGLE_QUARTER_TURN/TURN_RATE; i++) {
				ChangeActorAngle(0, GetActorAngle(0) - TURN_RATE);
				delay(1);
			}
			ChangeActorAngle(0, targetAngle);
			delay(2);
		}
		if (buttons & BT_FORWARD || buttons & BT_BACK || buttons & BT_MOVELEFT || buttons & BT_MOVERIGHT) {

			//Set the angle to move depending on the player's direction
			int step_x = 0.0; int step_y = 0.0;
			currentAngle = quantizeAngle(GetActorAngle(0));
            int oldSquareNum = getSquareFromPosition(GetActorX(0), GetActorY(0));
			targetAngle = currentAngle;
			int angleAdjustment = 0.0;
			if (buttons & BT_BACK) { angleAdjustment = 0.5; }
			if (buttons & BT_MOVELEFT) { angleAdjustment = 0.25; }
			if (buttons & BT_MOVERIGHT) { angleAdjustment = -0.25; }

			targetAngle += angleAdjustment;
			if (targetAngle >= 1.0) targetAngle -= 1.0;
			if (targetAngle < 0.0) targetAngle += 1.0;

			//Now work out the X and Y components from our angle
			switch (targetAngle) {
				case 0.0: step_x = 1.0; step_y = 0.0; break;
				case 0.25: step_x = 0.0; step_y = 1.0; break;
				case 0.5: step_x = -1.0; step_y = 0.0; break;
				case 0.75: step_x = 0.0; step_y = -1.0; break;
			}
			int playerX = GetActorX(0);
			int playerY = GetActorY(0);
			int playerZ = GetActorZ(0);

            bool canMove = true;
			//Check if next tile and points towards it are free
			for (i = 0.125; i <= 1.0; i += 0.125) {
                canMove &= ScriptCall("LevelHelper", "IsPointInLevel", playerX + step_x * FixedMul(TILE_SIZE, i), playerY + step_y * FixedMul(TILE_SIZE, i), GetActorZ(0) + (24<<16));
            }
            
            if (!canMove) { continue; }
            
			for (i = 0.125; i <= 1.0; i+= 0.125) {
				bool wasSpawned = SpawnForced("MapSpot", playerX + step_x * FixedMul(TILE_SIZE, i), playerY + step_y * FixedMul(TILE_SIZE, i), GetActorZ(0) + (16<<16), 999);
				int nextSquareZDiff = (GetActorFloorZ(999)>>16) - (GetActorZ(0)>>16);
				canMove &= !(nextSquareZDiff >= 32 || nextSquareZDiff <= -32);
				Thing_Remove(999);
			}

			if (!canMove) { continue; }
            
            if (ScriptCall("LevelHelper", "IsOnWetFloor")) { PlaySound(0, "po/treadwater", CHAN_AUTO); }
            else {                                           PlaySound(0, "po/tread", CHAN_AUTO);      }

            //Move the player forward, incrementing Z as needed
			for (i = 0; i < TILE_SIZE/STEP_RATE; i++) {
				bool moveSuccess = SetActorPosition(0, GetActorX(0) + step_x * STEP_RATE, GetActorY(0) + step_y * STEP_RATE, GetActorZ(0), false);
				if (!moveSuccess) {
					moveSuccess = SetActorPosition(0, GetActorX(0) + step_x * STEP_RATE, GetActorY(0) + step_y * STEP_RATE, GetActorZ(0) + (2<<16), false);
				}
				if (!moveSuccess) {
					moveSuccess = SetActorPosition(0, GetActorX(0) + step_x * STEP_RATE, GetActorY(0) + step_y * STEP_RATE, GetActorZ(0) + (8<<16), false);
				}
                if (!moveSuccess) {
					moveSuccess = SetActorPosition(0, GetActorX(0) + step_x * STEP_RATE, GetActorY(0) + step_y * STEP_RATE, GetActorZ(0) + (16<<16), false);
				}
                delay(1);
			}
			SetActorPosition(0, playerX + step_x * TILE_SIZE, playerY + step_y * TILE_SIZE, GetActorZ(0), false);

			//What's our new square?
			squareNum = getSquareFromPosition(GetActorX(0), GetActorY(0));
            
            //Move turn-based things in the level forward a step here
            ScriptCall("LevelHelper", "AdvanceTurnBasedTraps");
            
            //As a result of this move, are we on a damaging floor?
            if (ScriptCall("LevelHelper", "IsOnDamagingFloor")) {
                PlaySound(0, "po/damage", CHAN_AUTO);
                DamageActor(0, AAPTR_DEFAULT, 0, AAPTR_NULL, 5, "");
            }

			//As a result of this move, have we hit an exit?
			int exitMapNum = ScriptCall("LevelHelper", "HandleMapExit");
			if (exitMapNum) {
				FadeTo(0.0, 0.0, 0.0, 1.0, 1.0);
                PlaySound(0, "po/stairs", CHAN_AUTO);
				delay(35);
				Teleport_NewMap(exitMapNum, 0, 0);
				terminate;
			}
            
            //As a result of this move, have we hit an event?
            str scriptName = ScriptCall("LevelHelper", "HandleEvent");
            if (StrLen(scriptName) > 0) {
                ACS_NamedExecuteWait(scriptName, 0);
            }

			//As a result of this move, have we hit a FOE?
			int foeHit = ScriptCall("FOEHelper", "PlayerOverlapsAnyFOE");
			if (foeHit > 0) {
				//We've hit a FOE, so return to our square and go to an encounter!
                PlaySound(0, "po/foe/attack", CHAN_AUTO);
				SetActorPosition(0, playerX, playerY, playerZ, false);
				ACS_NamedExecute("beginEncounter", 0, foeHit, true);
				terminate; //Terminate here, as going back to this level will retrigger the script
			}

			//Now add the new square's danger value to the existing one
			int dangerValue = ScriptCall("DataLibrary", "ReadDangerValue", levelNum, squareNum);

			dangerValue = Random(max(0, dangerValue-5), dangerValue);

			GiveInventory("PoDanger", dangerValue);

			//If we're over 100 danger, this is an encounter! Time to pick a monster party
			if (CheckInventory("PoDanger") >= 100) {
				ACS_NamedExecute("beginEncounter", 0);
				terminate; //Terminate here, as going back to this level will retrigger the script
			}

			//Now move FOEs and start an encounter if one overlaps the player
			foeHit = ScriptCall("FOEHelper", "moveFOEs");
			if (foeHit > 0) {
                PlaySound(0, "po/foe/attack", CHAN_AUTO);
				ACS_NamedExecute("beginEncounter", 0, foeHit, true);
				terminate; //Terminate here, as going back to this level will retrigger the script
			}

			//We're safe!
			delay(2);
		}
	}
}

script "beginEncounter" (int forcedPopulation, int isFoe) {
	str monsterParty = "";
    
    //Load the next monster party data into data library
	if (forcedPopulation > 0) {
		monsterParty = ScriptCall("DataLibrary", "ChooseMonsterPartyFromPopulationID", StrParam(d:forcedPopulation));
	} else {
		monsterParty = ScriptCall("DataLibrary", "ChooseMonsterParty", levelNum, squareNum);
	}

	ScriptCall("DataLibrary", "WriteData", "FieldSquare", StrParam(d:squareNum));
	ScriptCall("DataLibrary", "WriteData", "FieldAngle", StrParam(d:currentAngle));
    
	FadeTo(1.0, 0.0, 0.0, 1.0, 0.0);
	SetMusic("POFIGHT");
	FadeTo(1.0, 0.0, 0.0, 0.0, 0.25);
    ACS_NamedExecute("displayEncounterText", 0, isFoe);
	Delay(5);
	FadeTo(0.0, 0.0, 0.0, 1.0, 1.0);
	delay(35);

	int arenaMapNum = ScriptCall("DataLibrary", "ReadInt", "ArenaMap");

	Teleport_NewMap(arenaMapNum, 0, 0);
}

script "displayEncounterText" (int isSpecialEncounter) {
    str partyName = ScriptCall("DataLibrary", "ReadData", "NextMonsterPartyName");
    if (isSpecialEncounter) {
        partyName = "Pain Elemental approaching!";
    }
	SetHudSize(800, 600, false);
	delay(5);
	PlaySound(0, "po/encounter", CHAN_AUTO);
	if (isSpecialEncounter) { SetFont("HXT_ALRT"); } else { SetFont("HXT_ENC"); } HudMessageBold(s:"A"; HUDMSG_PLAIN, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 10.0);
	delay(3);
	if (isSpecialEncounter) { SetFont("TXT_ALRT"); } else { SetFont("TXT_ENC"); } HudMessageBold(s:"A"; HUDMSG_PLAIN, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 10.0);

	delay(16);
	PlaySound(0, "po/punch", CHAN_AUTO);
	SetFont("BIGFONT"); HudMessageBold(s:partyName; HUDMSG_PLAIN, 998, CR_WHITE, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y + ENCOUNTER_NAME_Y_OFFSET, 10.0);
	delay(2);
	HudMessageBold(s:partyName; HUDMSG_PLAIN, 998, CR_GOLD, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y + ENCOUNTER_NAME_Y_OFFSET, 10.0);    
}

/////////////////////////////////////

script "setupArena" (void) {

	FadeTo(0.0, 0.0, 0.0, 1.0, 0.0);
    SetHudSize(800, 600, false);
    if (ScriptCall("DataLibrary", "ReadInt", "CurrentFOESquare") > 0) {
        SetFont("TXT_ALRT"); HudMessageBold(s:"A"; HUDMSG_FADEOUT, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 0.5, 0.5);
        SetFont("BIGFONT"); HudMessageBold(s:"Pain Elemental approaching!"; HUDMSG_FADEOUT, 998, CR_GOLD, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y + ENCOUNTER_NAME_Y_OFFSET, 0.5, 0.5);  
    }
    else {
        SetFont("TXT_ENC"); HudMessageBold(s:"A"; HUDMSG_FADEOUT, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 0.5, 0.5);
        SetFont("BIGFONT"); HudMessageBold(s:ScriptCall("DataLibrary", "ReadData", "NextMonsterPartyName"); HUDMSG_FADEOUT, 998, CR_GOLD, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y + ENCOUNTER_NAME_Y_OFFSET, 0.5, 0.5);  
    }
	FadeTo(0.0, 0.0, 0.0, 0.0, 0.5);

	str chosenMonsterParty = ScriptCall("DataLibrary", "ReadData", "NextMonsterParty");

	int numberOfStarts = ThingCountName("BattleStart", 0);
	int arenaTid = 100 * (Random(0, numberOfStarts - 1) + 1);

    ScriptCall("LevelHelper", "PrepareWeapons");

	int battleStartTid = arenaTid + 1;

	Thing_Move(0, battleStartTid, true);
	SetActorAngle(0, GetActorAngle(battleStartTid));

	//If this is a FOE fight, do a bit extra...
	if (ScriptCall("DataLibrary", "ReadInt", "CurrentFOESquare") > 0) {
		SpawnSpotFacing("FOE", arenaTid, 999);
		delay(20);
		SetActorState(999, "Destroy");
		delay(5);
	}

	SetPlayerProperty(1, 0, PROP_FROZEN);

	//Spawn the monsters meant to go into each slot in this arena!
	for (int i = 0; i < 10; i++) {
		str monsterClass = ScriptCall("DataLibrary", "ReadMonsterParty", chosenMonsterParty, StrParam(d:i));
		if (StrLen(monsterClass) > 0) {
			SpawnSpotFacing(monsterClass, arenaTid + i + 2, 999);
			SpawnSpot("TeleportFog", arenaTid + i + 2);
			delay(Random(5, 10));
		}
	}
	NoiseAlert(0, 0);

	while (ThingCount(T_NONE, 999) > 0) {
		delay(35);
	}

	ACS_NamedExecute("displayClearText", 0);
    TakeInventory("PoDanger", CheckInventory("PoDanger"));
	delay(35 * 4);

	FadeTo(0.0, 0.0, 0.0, 1.0, 1.0);
	int vol = 1.0;
	for (i = 0; i < 35; i++) {
		vol -= 0.05;
		SetMusicVolume(vol);
		delay(1);
	}
    ScriptCall("LevelHelper", "CleanupArena");
	Thing_Remove(999);
    SetPlayerProperty(1, 1, PROP_FROZEN);
    ScriptCall("LevelHelper", "StopActor");
    delay(1);

	//Return to previous map
	int mapToReturn = ScriptCall("DataLibrary", "ReadInt", "FieldMap");
	Teleport_NewMap(mapToReturn, 0, 0);
}

script "displayClearText" (void) {
	SetHudSize(800, 600, false);
	PlaySound(0, "po/victory", CHAN_AUTO);
	SetFont("HXT_CLR"); HudMessageBold(s:"A"; HUDMSG_PLAIN, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 10.0);
	delay(3);
	SetFont("TXT_CLR"); HudMessageBold(s:"A"; HUDMSG_FADEOUT, 999, CR_UNTRANSLATED, ENCOUNTER_TEXT_X, ENCOUNTER_TEXT_Y, 3.0, 0.5); 
}


//////////////////////////////////

function int getSquareFromPosition(int x, int y) {
	int sqNum = 10000;
	sqNum += ((x>>16) / TILE_SIZE);
	sqNum += ((y>>16)*100 / TILE_SIZE);
	return sqNum;
}

function void getPositionFromSquare(int sqNum) {
	sqNum -= 10000;
	registerX = ((sqNum % 100) * TILE_SIZE) << 16;
	sqNum /= 100;
	registerY = ((sqNum % 100) * TILE_SIZE) << 16;
	SpawnForced("MapSpot", registerX, registerY, 0, 999);
	registerZ = GetActorFloorZ(999);
	Thing_Remove(999);
}

function int quantizeAngle(int angle) {
	int x = round(FixedMul(angle, 4.0));
	//Switch here to avoid fixed division errors
	switch (x) {
		case 0: case 4 << 16: x = 0; break;
		case 1 << 16: x = 0.25; break;
		case 2 << 16: x = 0.5; break;
		case 3 << 16: x = 0.75; break;
	}
	return x;
}

function int max (int a, int b) {
	return (a > b) ? a : b;
}