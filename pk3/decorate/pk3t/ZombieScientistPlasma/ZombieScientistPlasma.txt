ACTOR ZombieScientistPlasma
{
  //$Category Monsters
  Tag "Zombie Scientist Plasma"
  obituary "%o was melted by a zombie scientist."
  health 50
  mass 100
  speed 16
  Radius 20
  Height 52
  painchance 150
  seesound "scientist/sight"
  painsound "scientist/pain"
  deathsound "scientist/death"
  activesound "scientist/active"
  MONSTER
  +FLOORCLIP
  +AVOIDMELEE
  +MISSILEMORE
  DropItem Cell
  States
  {
  Spawn:
    SCZP AB 10 A_Look
    loop
  See:
    SCZP AABBCCDD 3 A_Chase
    loop
  Missile:
    SCZP E 6 A_FaceTarget
    SCZP E 1 A_PlaySound ("PlasmaHi/Fire")
	SCZP F 5 A_CustomMissile ("ZombiePlasmaBall", 45, 3, (1)*Random(-3, 3), CMF_OFFSETPITCH, (0.1)*Random(-3, 3))
	SCZP E 12
    goto See
  Pain:
    SCZP G 3
    SCZP G 3 A_Pain
    goto See
  Death:
    SCZP H 5 A_SpawnItemEx ("ZombiePlasmaDeathExplosion", 0.0, 0.0, 32.0, 0.0, 0.0, 0.0, 0.0, SXF_NOCHECKPOSITION, 0)
    SCZP I 5 A_Scream
    SCZP J 5 A_NoBlocking
    SCZP K 5
    SCZP L 5
    SCZP M 5
    SCZP N -1
    stop
  XDeath:
    SCZP O 5 A_SpawnItemEx ("ZombiePlasmaDeathExplosion", 0.0, 0.0, 32.0, 0.0, 0.0, 0.0, 0.0, SXF_NOCHECKPOSITION, 0)
    SCZP P 5 A_XScream
    SCZP Q 5 A_NoBlocking
    SCZP RSTUV 5
    SCZP W -1
    stop
  Raise:
    SCZP MLKJIH 5
    goto See
  }
}

Actor ZombiePlasmaBall
{
  Height 8
  Radius 6
  Speed 15
  FastSpeed 25
  Projectile
  RenderStyle Add
  Damage (10 + random(1,5))
  Scale 0.4
  DeathSound "weapons/plasmax"
  Decal PlasmaScorch
  States
  {
  Spawn:
    ZPLS AB 2 BRIGHT
	loop
  Death:
    PLSE ABCDE 4 Bright
	Stop
  }
}


//Why can we get his plasma gun?
//Because it went up in a flash of plasma when he died, that's why. :P
ACTOR ZombiePlasmaDeathExplosion
{
  Radius 1
  Height 2
  +NOGRAVITY
  RenderStyle Add
  Alpha 0.75
  Scale 0.5
  States
  {
  Spawn:
    ZPLX A 0
    ZPLX A 4 A_PlaySound("weapons/plasmax")
    ZPLX BCDE 4 Bright
    Stop
  }
}
